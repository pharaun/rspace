use std::str::FromStr;
use types;

grammar;

match {
    r"(CYCLE|TIME|INSTRET)H?",
} else {
    _
}

// number == digits + Ox{digits} (does not handle negative number) unsigned int32/64) (later can
//                              handle negative by converting it to 2's compat and  storing it as unsigned)
pub Number = { Dec, Hex };

Dec: u32 = <s:r"[0-9]+"> => u32::from_str(s).unwrap();
Hex: u32 = <s:r"0x[0-9A-Fa-f]+"> => u32::from_str_radix(&s[2..], 16).unwrap();

// Register == letter + digits
// For now only support x{0-31} styled registers
pub Register = { Reg };

Reg: &'input str = <s:r"x[0-9]+"> => s;

// CSR & priv inst
// csr == ?
// For now, support a discrete list of csr + priv insts
pub Csr = { CsrReg };

CsrReg: &'input str = <s:r"(CYCLE|TIME|INSTRET)H?"> => s;

// Labels (local label)
pub Label = { LLabel, WLabel };

LLabel: &'input str = <s:r"[0-9]+[BFbf]"> => s;

// Label == letters (but using . to make inst happy)
WLabel: &'input str = <s:r"[A-Za-z.]+"> => s;

// args = register | number | csr
pub Arguments: types::Args <'input> = {
    <n:Register> => types::Args::Reg(n),
    <n:Csr> => types::Args::Csr(n),
    <n:Number> => types::Args::Num(n),
    <n:Label> => types::Args::Lab(n),
};

// [0-4] args
pub VecArgs: Vec<types::Args <'input>> = {
    <Arguments*>
};

// Asm Line = Instruction [0-4] args
// TODO:
//  gcc as assemblier uses a slightly different syntax:
//      - add x0, x1, x3
//      - lw x0, 0x0(x3)
//      - sw x0, 0x0(x3)
//      - csr{rw, rs, rc} a0, cycle, x0
//      - csr{rw, rs, rc}i a1, sscratch, 1
pub AsmLine: types::AsmLine <'input> = {
    <l:r"[0-9]+"> ":" <i:WLabel> <v:VecArgs> => types::AsmLine::Lns(l, i, v),
    <l:WLabel> ":" <i:WLabel> <v:VecArgs>    => types::AsmLine::Lns(l, i, v),
    //Instruction VecArgs
    <i:WLabel> <v:VecArgs>                   => types::AsmLine::Ins(i, v),
    <i:r"[0-9]+"> ":"                        => types::AsmLine::Lab(i),
    <i:WLabel> ":"                           => types::AsmLine::Lab(i),
};

// Instruction == letter + .
//Instruction: &'input str = <s:r"[A-Za-z.]+"> => s;
